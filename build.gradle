import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'net.oskarstrom.dashloader'
version '3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api libs.activej.serializer
    shadow libs.bundles.activej
    shadow libs.slf4j.api

    implementation libs.snakeyaml
    shadow libs.snakeyaml
    implementation libs.fastutil

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.oskarstrom.dashloader.shadowed"
}

tasks.shadowJar.dependsOn relocateShadowJar

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}